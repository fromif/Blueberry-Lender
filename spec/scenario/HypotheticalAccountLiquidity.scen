
Test "Calculates hypothetical account liquidity"
    -- Note, this comes a bit from `EnterExitMarkets` Scenario
    PricedComptroller
    ListedBToken ZRX bZRX initialExchangeRate:1e9
    SetPriceCF bZRX 0.002 0.4
    ListedEtherToken bETH initialExchangeRate:1e9
    Comptroller SetCollateralFactor bEth 0.8
    ListedBToken BAT bBAT initialExchangeRate:1e9
    SetPriceCF bBAT 0.0015 0.3
    ListedBToken OMG bOMG initialExchangeRate:1e9
    NewBToken REP bREP
    -- Mint some bZRX and bETH
    Prep Geoff 1250e18 ZRX bZRX
    Mint Geoff 1250e18 bZRX -- Liquidity -> 0.4 * 0.002 * 1250e18 = 1.0e18
    SendMintEth Geoff 2.5e18 bETH -- Liqiuidity -> 0.8 * 1.0 * 2.5e18 = 2.0e18
    -- Check liquidity is zero when not in any markets
    Assert Equal (Comptroller Liquidity Geoff) Zero
    -- Enter ZRX and check liquidity
    EnterMarkets Geoff bZRX
    Assert Equal (Comptroller Liquidity Geoff) 1.0e18
    -- Now let's look at some hypotheticals
    Assert Equal (Comptroller Hypothetical Geoff Redeems 1000e9 bZRX) 0.2e18 -- 0.4 * 0.002 * -1000e18 = -0.8e18
    Assert Equal (Comptroller Hypothetical Geoff Borrows 1000e18 bZRX) -1e18 -- 0.002 * -1000e18 = -2e18
    -- Note, if you're not in an asset, this function returns no effect
    Assert Equal (Comptroller Hypothetical Geoff Redeems 0.002e9 bETH) 1.0e18 -- 0.8 * 1000.0 * -0.002e18 = -1.6e18
    Assert Equal (Comptroller Hypothetical Geoff Borrows 0.002e18 bETH) 1.0e18 -- 1000.0 * -0.002e18 = -2e18
    EnterMarkets Geoff bETH
    Assert Equal (Comptroller Liquidity Geoff) 3.0e18
    Assert Equal (Comptroller Hypothetical Geoff Redeems 1000e9 bZRX) 2.2e18 -- 0.4 * 0.002 * -1000e18 = -0.8e18
    Assert Equal (Comptroller Hypothetical Geoff Borrows 1000e18 bZRX) 1e18 -- 0.002 * -1000e18 = -2e18
    Assert Equal (Comptroller Hypothetical Geoff Redeems 2e9 bETH) 1.4e18 -- 0.8 * 1.0 * -2e18 = -1.6e18
    Assert Equal (Comptroller Hypothetical Geoff Borrows 2e18 bETH) 1.0e18 -- 1.0 * -2e18 = -2e18
    EnterMarkets Geoff bBAT
    Assert Equal (Comptroller Hypothetical Geoff Redeems 1000e9 bBAT) 2.55e18 -- 0.3 * 0.0015 * -1000e18 = -0.45e18e18
    Assert Equal (Comptroller Hypothetical Geoff Borrows 1000e18 bBAT) 1.5e18 -- 0.0015 * -1000e18 = -1.5e18
    EnterMarkets Geoff bOMG
    -- Fails if the given asset doesn't have a price
    Assert ReadRevert (Comptroller Hypothetical Geoff Redeems 1e20 bOMG) "revert price error"
    Assert ReadRevert (Comptroller Hypothetical Geoff Borrows 1e20 bOMG) "revert price error"
    PriceOracle SetPrice bOMG 0.01
    -- Has a price and now is listed
    Assert Equal (Comptroller Hypothetical Geoff Redeems 100e9 bOMG) 3.0e18 -- No collateral factor
    Assert Equal (Comptroller Hypothetical Geoff Borrows 100e18 bOMG) 2.0e18 -- 0.01 * -100e18 = -1e18
    -- For a token has no price and isn't listed
    -- Note: we can't actually enter the unlisted market without some harness function
    --       we could consider testing this, but it would be out of the scope of scenarios
    Assert Equal (Comptroller Hypothetical Geoff Redeems 100e18 bREP) 3.0e18 -- No effect
    Assert Equal (Comptroller Hypothetical Geoff Borrows 100e18 bREP) 3.0e18 -- No effect
