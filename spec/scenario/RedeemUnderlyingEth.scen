-- Redeem Tests

Test "Mint then Redeem All"
    NewComptroller
    ListedEtherToken bETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 bETH
    -- Check current affairs
    Assert Equal (Erc20 bETH TokenBalance Geoff) 10e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 0.005e18
    Invariant Static (BToken bETH ExchangeRateStored)
    -- Now redeem after some time
    FastForward 2 Blocks
    Expect Changes (EtherBalance Geoff) +0.005e18
    RedeemUnderlyingEth Geoff 0.005e18 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) Zero
    Assert Equal (BToken bETH UnderlyingBalance Geoff) Zero

Test "Mint then Redeem Part"
    NewComptroller
    ListedEtherToken bETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 bETH
    -- Check current affairs
    Assert Equal (Erc20 bETH TokenBalance Geoff) 10e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 0.005e18
    Invariant Static (BToken bETH ExchangeRateStored)
    -- Now redeem after some time
    FastForward 2 Blocks
    Expect Changes (EtherBalance Geoff) +0.001e18
    RedeemUnderlyingEth Geoff 0.001e18 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) 8e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 0.004e18

Test "Mint then Redeem Too Much"
    NewComptroller
    ListedEtherToken bETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 bETH
    AllowFailures
    -- Check and hold static
    Invariant Static (BToken bETH ExchangeRateStored)
    Invariant Remains (Erc20 bETH TokenBalance Geoff) 10e8
    Invariant Static (EtherBalance Geoff)
    -- Now redeem after some time
    FastForward 2 Blocks
    RedeemUnderlyingEth Geoff 0.0055e18 bETH
    Assert Revert "revert subtraction underflow"

Test "Mint then Redeem Zero"
    NewComptroller
    ListedEtherToken bETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 bETH
    -- Check and hold static
    Invariant Static (BToken bETH ExchangeRateStored)
    Invariant Remains (Erc20 bETH TokenBalance Geoff) 10e8
    Invariant Static (EtherBalance Geoff)
    -- Now redeem after some time
    FastForward 2 Blocks
    RedeemUnderlyingEth Geoff 0e18 bETH

Test "Mint then redeem with interest - no reserves"
    Invariant Success
    NewComptroller
    ListedEtherToken bETH initialExchangeRate:1e9
    Invariant Remains (BToken bETH Reserves) Zero
    CallMintEth Geoff 50e18 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) 500e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 50e18
    Assert Equal (BToken bETH ExchangeRate) 1e9
    -- Get some brah to borrow then repay
    BorrowAndRepayEthWithInterest bETH 10e18 5e18 interestRate:0.000001 blocks:500000
    -- We've accrued 10% interest for 5 blocks, or 50% of the amount,
    --   thus, we should have accrued 5e18 of interest for the protocol
    --   This is due pro-rata to all holders, but we just have one, so
    --   let's check that account is given correct new balance.
    Assert Equal (Erc20 bETH TokenBalance Geoff) 500e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 55e18
    Assert Equal (BToken bETH ExchangeRate) 1.1e9
    -- Now redeem all with interest
    Expect Changes (EtherBalance Geoff) 55e18
    RedeemUnderlyingEth Geoff 55e18 bETH
    Assert Equal (EtherBalance bETH) 0e18
    Assert Equal (Erc20 bETH TokenBalance Geoff) 0e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 0e18
    Assert Equal (BToken bETH ExchangeRate) 1e9

Pending "Mint then redeem part with interest - no reserves"
    Invariant Success
    NewComptroller
    ListedBToken ZRX bETH initialExchangeRate:1e9
    Invariant Remains (BToken bETH Reserves) Zero
    Prep Geoff 50e18 ZRX bETH
    Mint Geoff 50e18 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) 500e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 50e18
    Assert Equal (BToken bETH ExchangeRate) 1e9
    -- Get some brah to borrow then repay
    BorrowAndRepayWithInterest ZRX bETH 10e18 5e18 interestRate:0.000001 blocks:500000
    -- We've accrued 10% interest for 5 blocks, or 50% of the amount,
    --   thus, we should have accrued 5e18 of interest for the protocol
    --   This is due pro-rata to all holders, but we just have one, so
    --   let's check that account is given correct new balance.
    Assert Equal (Erc20 bETH TokenBalance Geoff) 500e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 55e18
    Assert Equal (BToken bETH ExchangeRate) 1.1e9
    -- Now redeem all with interest
    Redeem Geoff 499e8 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) 1e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 0.11e18
    Assert Equal (BToken bETH ExchangeRate) 1.1e9

Pending "Mint then redeem with reserves and interest"
    Invariant Success
    NewComptroller
    ListedBToken ZRX bETH initialExchangeRate:1e9
    Prep Geoff 50e18 ZRX bETH
    Mint Geoff 50e18 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) 500e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 50e18
    Assert Equal (BToken bETH ExchangeRate) 1e9
    -- Get some brah to borrow then repay
    BorrowAndRepayWithInterest ZRX bETH 10e18 5e18 interestRate:0.000001 blocks:500000 reserveRate:0.2
    -- We've accrued 10% interest for 5 blocks, or 50% of the amount,
    --   thus, we should have accrued 5e18 of interest for the protocol
    --   The reserves should get 20% of this, or 1e18, and the rest
    --   is due pro-rata to all holders. We just have one, so
    --   let's check that account is given correct new balance.
    Assert Equal (Erc20 bETH TokenBalance Geoff) 500e8
    Assert Equal (BToken bETH Reserves) 1e18
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 54e18
    -- 55e18 + 0e18 - 1e18 / 500
    Assert Equal (BToken bETH ExchangeRate) 1.08e9
    -- Now redeem all with interest
    Redeem Geoff 500e8 bETH
    Assert Equal (Erc20 ZRX TokenBalance Geoff) 54e18
    Assert Equal (Erc20 ZRX TokenBalance bETH) 1e18
    Assert Equal (Erc20 bETH TokenBalance Geoff) 0e8
    Assert Equal (BToken bETH Reserves) 1e18
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 0e18
    Assert Equal (BToken bETH ExchangeRate) 1e9

Test "Two users Mint, one redeems"
    NewComptroller
    ListedEtherToken bETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.002e18 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) 4e8
    Assert Equal (Erc20 bETH TotalSupply) 4e8
    CallMintEth Torrey 0.004e18 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) 4e8
    Assert Equal (Erc20 bETH TokenBalance Torrey) 8e8
    Assert Equal (Erc20 bETH TotalSupply) 12e8
    RedeemUnderlyingEth Torrey 0.0015e18 bETH
    Assert Equal (Erc20 bETH TokenBalance Geoff) 4e8
    Assert Equal (Erc20 bETH TokenBalance Torrey) 5e8
    Assert Equal (Erc20 bETH TotalSupply) 9e8

Test "Mint then redeem 1 wei"
    NewComptroller
    ListedEtherToken bETH initialExchangeRate:0.005e9
    CallMintEth Geoff 0.005e18 bETH
    AllowFailures
    -- Check current affairs
    Assert Equal (Erc20 bETH TokenBalance Geoff) 10e8
    Assert Equal (BToken bETH UnderlyingBalance Geoff) 0.005e18
    Invariant Static (BToken bETH ExchangeRateStored)
    Invariant Static (EtherBalance Geoff)
    -- Now redeem after some time
    FastForward 2 Blocks
    RedeemUnderlyingEth Geoff 1 bETH
    Assert Revert "revert redeemTokens zero"
