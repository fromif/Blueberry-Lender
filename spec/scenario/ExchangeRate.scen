
Test "Initial Exchange Rate"
    NewComptroller
    NewBToken ZRX bZRX initialExchangeRate:2e9
    Assert Equal (BToken bZRX ExchangeRateStored) (Exp 2e9)

Test "Initial Exchange Rate with Mint"
    NewComptroller
    ListedBToken ZRX bZRX initialExchangeRate:5e9
    -- Check initial exchange holds
    Invariant Remains (BToken bZRX ExchangeRateStored) (Exp 5e9)
    -- Mint some tokens and verify invariant still holds
    Prep Geoff Some ZRX bZRX
    Mint Geoff 50e18 bZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Erc20 bZRX TotalSupply) (Exactly 10e9)
    Assert Equal (Erc20 bZRX TokenBalance Geoff) (Exactly 10e9)
    -- Let's mint again and verify it's still good.
    Prep Torrey Some ZRX bZRX
    Mint Torrey 50e18 bZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Erc20 bZRX TotalSupply) (Exactly 20e9)
    Assert Equal (Erc20 bZRX TokenBalance Torrey) (Exactly 10e9)

Test "ZRX: Exch. Rate:2e9, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewComptroller
    -- Decimals You=18, Decimals Us=8 -> 2e9 Exchange Rate
    ListedBToken ZRX bZRX initialExchangeRate:2e9 decimals:8
    Assert Equal (BToken bZRX ExchangeRateStored) (Exp 2e9)
    -- Mint 50.0 ZRX at given exchange rate
    Prep Geoff Some ZRX bZRX
    Assert Equal (Erc20 bZRX TotalSupply) Zero
    Mint Geoff 50e18 bZRX
    -- 50e18 / 2e9 = 2.5e10
    Assert Equal (Erc20 bZRX TotalSupply) (Exactly 2.5e10)
    Assert Equal (Erc20 bZRX TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    GiveBToken bZRX 1.0e18 ZRX -- Faucet some ZRX
    Assert Equal (Erc20 ZRX TokenBalance bZRX) (Exactly 51.0e18)
    -- Mock total borrows
    BToken bZRX Mock totalBorrows 2.0e18
    Assert Equal (BToken bZRX TotalBorrows) (Exactly 2.0e18)
    -- Mock total reserves
    BToken bZRX Mock totalReserves 0.5e18
    Assert Equal (BToken bZRX Reserves) (Exactly 0.5e18)
    -- Okay, we're all set, let's check the exchange rate
    -- (51+2-0.5)e18/2.5e10 = 52.5e18/2.5e10 = 21e8 -> 21e26 (Exp)
    Assert Equal (BToken bZRX ExchangeRateStored) (Exp 2.1e9)

Test "USDC: Exch. Rate:2e-3, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewComptroller
    -- Decimals You=6, Decimals Us=8 -> 2e-3 Exchange Rate
    ListedBToken USDC bUSDC initialExchangeRate:2e-3 decimals:8
    Assert Equal (BToken bUSDC ExchangeRateStored) (Exp 2e-3)
    -- Mint 50.0 USDC at given exchange rate
    Prep Geoff Little USDC bUSDC
    Assert Equal (Erc20 bUSDC TotalSupply) (Exactly 0.0)
    Mint Geoff 50e6 bUSDC
    -- 5.0e7 / 2e-3 = 2.5e10
    Assert Equal (Erc20 bUSDC TotalSupply) (Exactly 2.5e10)
    Assert Equal (Erc20 bUSDC TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    GiveBToken bUSDC 2.0e6 USDC -- Faucet some USDC
    Assert Equal (Erc20 USDC TokenBalance bUSDC) (Exactly 52.0e6)
    -- Mock total borrows
    BToken bUSDC Mock totalBorrows 5.0e6
    Assert Equal (BToken bUSDC TotalBorrows) (Exactly 5.0e6)
    -- Mock total reserves
    BToken bUSDC Mock totalReserves 1.5e6
    Assert Equal (BToken bUSDC Reserves) (Exactly 1.5e6)
    -- Okay, we're all set, let's check the exchange rate
    -- (52+5-1.5)e6/2.5e10 = 55.5e6/2.5e10 = 2.22e-3 -> 2.22e15 (Exp)
    Assert Equal (BToken bUSDC ExchangeRateStored) (Exp 2.22e-3)
