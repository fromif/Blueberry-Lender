
exchangeRateDecreases(uint result, address minter, uint256 mintAmount, uint256 mintTokens) {
    // Pre/action/post environments
    env e0;
    env e1;
    env e2;

    require e1.block.number >= e0.block.number;
    require e2.block.number >= e1.block.number;

    // Any other account
    address other;
    require other != minter && other != currentContract;
    require minter != currentContract;

    uint256 exchangeRatePre = sinvoke exchangeRateCurrent(e0);

    uint256 bTokenCashPre = sinvoke getCash(e0);
    uint256 bTokenBorrowsPre = sinvoke totalBorrows(e0);
    uint256 bTokenTokensPre = sinvoke totalSupply(e0);
    uint256 bTokenReservesPre = sinvoke totalReserves(e0);
    uint256 bTokenSupplyPre = bTokenCashPre + bTokenBorrowsPre - bTokenReservesPre;

    // Simplifying assumptions to analyze the vulnerability
    require bTokenBorrowsPre == 0; // XXX not necessary, simplifies analysis
    require bTokenSupplyPre >= 0; // XXX not necessary, should underflow
    require bTokenTokensPre > 0; // XXX not necessary
    require exchangeRatePre * bTokenTokensPre == bTokenSupplyPre * 1000000000000000000; // XXX

    // XXX why does this have to be after require?
    uint256 implicitExchangeRatePre = bTokenSupplyPre * 1000000000000000000 / bTokenTokensPre;

    require result == invoke mintFreshPub(e1, minter, mintAmount);
    bool mintFreshReverted = lastReverted;

    uint256 exchangeRatePost = sinvoke exchangeRateCurrent(e2);

    uint256 bTokenCashPost = sinvoke getCash(e2);
    uint256 bTokenBorrowsPost = sinvoke totalBorrows(e2);
    uint256 bTokenTokensPost = sinvoke totalSupply(e2);
    uint256 bTokenReservesPost = sinvoke totalReserves(e2);
    uint256 bTokenSupplyPost = bTokenCashPost + bTokenBorrowsPost - bTokenReservesPost;

    require mintTokens == bTokenTokensPost - bTokenTokensPre;
    require exchangeRatePre * mintTokens == mintAmount * 1000000000000000000;  // XXX why would this need to be assumed? should be proven

    uint256 implicitExchangeRatePost = bTokenSupplyPost * 1000000000000000000 / bTokenTokensPost;

    assert (!mintFreshReverted =>
            ((result != 0) => (exchangeRatePost == exchangeRatePre))), "Mismatch in failure case";
    assert (!mintFreshReverted => (exchangeRatePost >= exchangeRatePre)), "Exchange rate decreased";
    assert (!mintFreshReverted => (implicitExchangeRatePost >= implicitExchangeRatePre)), "Implicit exchange rate decreased";
}
